OutputTerm = 3	#1:PNG, 2:EPS, 3:SVG
Env = "ku2022"
Tag = "SPROX"
BaseName = sprintf("%s_%s_3D", Env, Tag)
set encoding utf8

if (OutputTerm  == 1){
	set term pngcairo color size 2000,2000 enhanced font ",24"
	set output ("fig/".BaseName)."_1.png"
}
if (OutputTerm  == 2){
	set term postscript eps color size 4.5in,3in enhanced font ",18"
	set output ("fig/".BaseName)."_1.eps"
}
if (OutputTerm  == 3){
	set term svg size 1000,1000 font ",30"
	set output ("fig/".BaseName)."_1.svg"
}

set xlabel 'x [m]' #offset 0,1
set ylabel 'y [m]' #offset 1.5,0

#set tmargin 0
#set bmargin 0
#set lmargin 0
#set rmargin 0
#set size 0.85,0.85
#set origin 0.13,0.13
#set xtics offset 0,0.4
#set ytics offset 0.4,0

angle = 0*pi/180
X(x,y) = (x*cos(angle)-y*sin(angle))
Y(x,y) = (x*sin(angle)+y*cos(angle))

set xrange [-40:140]
set yrange [-180:20]
set xtics 40
set ytics 40
#set noztics

array Z[6] = [5, 4, 3, 2, 1, 0]
array proxClr[6] = ["#ff8080", "#c0c080", "#80ff80", "#80c0c0", "#8080ff", "#c080c0"]
array locClr[6]  = ["#ff0000", "#ffff00", "#00ff00", "#00ffff", "#0000ff", "#ff00ff"]

#set nokey
#set key font"Arial,20"
set key left top
#set key right top
#set key right bottom
#set key at 50,-50
set key invert

array loopPair[34] = [1,1, 1,2, 2,2, 1,3, 2,3, 3,3, 2,4, 3,4, 4,4, 4,5, 5,5, \
                      1,6, 2,6, 3,6, 4,6, 5,6, 6,6]
array loopClr[17] = ["#ff0000", "#ff8000", "#f0f000", "#808000", "#80ff00", \
                     "#00ff00", "#80ff80", "#00ff80", "#00ffff", "#0080ff", "#0000ff", \
                     "#ff8080", "#ffff00", "#80ff80", "#00ffff", "#8080ff", "#ff00ff"]

set zrange [ Z[5]:Z[1] ]
set nozlabel
unset ztics
set ticslevel 0

set view equal xy
set view 60, 45, 1.2, 0.6

splot for [i = 1:11] sprintf("..\/data\/%s_1%d_1%d_%s_absproxmatch.csv", Env, loopPair[2*i-1], loopPair[2*i], Tag) every :10 using (X($4,$5)):(Y($4,$5)):($1 > 0 ? Z[$1] : 1/0) with lines lw 0.5 lc rgb loopClr[i] notitle, \
	for [i = 5:1:-1] sprintf("..\/data\/%s_1%d_%s_absprox.csv", Env, i, Tag) every :10 using (X($3,$4)):(Y($3,$4)):(Z[i]) with point ps 0.3 pt 7 lc rgb proxClr[i] notitle, \
	for [i = 5:1:-1] sprintf("..\/data\/%s_1%d_%s_loc.csv", Env, i, Tag) every 5 using (X($3,$4)):(Y($3,$4)):(Z[i]) with lines lw 7 lc rgb locClr[i] notitle



if (OutputTerm  == 1){
	set term pngcairo color size 2000,2000 enhanced font ",24"
	set output ("fig/".BaseName)."_2.png"
}
if (OutputTerm  == 2){
	set term postscript eps color size 4.5in,3in enhanced font ",18"
	set output ("fig/".BaseName)."_2.eps"
}
if (OutputTerm  == 3){
	set term svg size 1000,1000 font ",30"
	set output ("fig/".BaseName)."_2.svg"
}

do for [i = 1:5]{
	Z[i] = 5
}
set zrange [ Z[6]:Z[1] ]
set view 60, 45, 1.2, 0.15

splot sprintf("..\/data\/%s_1%d_%s_absprox.csv", Env, 6, Tag) every :10 using (X($3,$4)):(Y($3,$4)):(Z[6]) with point ps 0.3 pt 7 lc rgb proxClr[6] notitle, \
	sprintf("..\/data\/%s_1%d_%s_loc.csv", Env, 6, Tag) every 5 using (X($3,$4)):(Y($3,$4)):(Z[6]) with lines lw 7 lc rgb locClr[6] notitle, \
	for [i = 12:17] sprintf("..\/data\/%s_1%d_1%d_%s_absproxmatch.csv", Env, loopPair[2*i-1], loopPair[2*i], Tag) every :10 using (X($4,$5)):(Y($4,$5)):($1 > 0 ? Z[$1] : 1/0) with lines lw 0.5 lc rgb loopClr[i] notitle, \
	for [i = 5:1:-1] sprintf("..\/data\/%s_1%d_%s_absprox.csv", Env, i, Tag) every :10 using (X($3,$4)):(Y($3,$4)):(Z[i]) with point ps 0.3 pt 7 lc rgb proxClr[i] notitle, \
	for [i = 5:1:-1] sprintf("..\/data\/%s_1%d_%s_loc.csv", Env, i, Tag) every 5 using (X($3,$4)):(Y($3,$4)):(Z[i]) with lines lw 7 lc rgb locClr[i] notitle
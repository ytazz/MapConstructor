OutputTerm = 3
Mono = 0
Env = "nc2021"
#Tag = "SPROX"
Tag = "SE2"
#Tag = "Huber"
#Tag = "GM"
BaseName = (Env."_").Tag
set encoding utf8
angle = 120*pi/180
X(x,y) = (x*cos(angle)-y*sin(angle))
Y(x,y) = (x*sin(angle)+y*cos(angle))

if (OutputTerm  == 1){
	set term pngcairo color size 2000,2000 enhanced font ",24"
	set output ("fig/".BaseName).".png"
}
if (OutputTerm  == 2){
	set term postscript eps color size 4.5in,3in enhanced font ",18"
	set output ("fig/".BaseName).".eps"
}
if (OutputTerm  == 3){
	set term svg size 1000,1000 font ",30"
	set output ("fig/".BaseName).".svg"
}

set xlabel 'x [m]' offset 0,1
set ylabel 'y [m]' offset 1.5,0

set tmargin 0
set bmargin 0
set lmargin 0
set rmargin 0
set size 0.85,0.85
set origin 0.13,0.13
set xtics offset 0,0.4
set ytics offset 0.4,0

#set nokey
set xrange [-75:150]
set yrange [-150:50]
set xtics 50
set ytics 50
set key right top
#set key left top
#set key right bottom
#set key at 50,-70
#set key invert
set size ratio -1

#array proxClr[6] = ["#ff8080", "#c0c080", "#80ff80", "#80c0c0", "#8080ff", "#c080c0"]
#array locClr[6]  = ["#ff0000", "#ffff00", "#00ff00", "#00ffff", "#0000ff", "#ff00ff"]
array proxClr[3] = ["#ff8080", "#c080c0", "#8080ff"]
array geoClr[3] = ["#00c040", "#00ff00", "#40c000"]
array locClr[3]  = ["#ff0000", "#ff00ff", "#0000ff"]

plot for [i = 3:1:-1] sprintf("..\/data\/%s_%d_%s_absprox.csv", Env, i, Tag) every 3 using (X($3,$4)):(Y($3,$4)) with point ps 0.05 pt 7 lc rgb proxClr[i] notitle, \
	for [i = 3:1:-1] sprintf("..\/data\/%s_%d_locgeo.csv", Env, i) every 1 using (X($3,$4)):(Y($3,$4)) with lines lw 3 lc rgb geoClr[i] notitle, \
	for [i = 3:1:-1] sprintf("..\/data\/%s_%d_%s_loc.csv", Env, i, Tag) every 1 using (X($3,$4)):(Y($3,$4)) with lines lw 3 lc rgb locClr[i] notitle

#plot for [i = 6:1:-1] sprintf("..\/data\/%s_1%d_%s_absprox.csv", Env, i, Tag) every :5 using (X($3,$4)):(Y($3,$4)) with point ps 0.3 pt 7 lc rgb proxClr[i] notitle, \
#	for [i = 6:1:-1] sprintf("..\/data\/%s_1%d_%s_loc.csv", Env, i, Tag) every 5 using (X($3,$4)):(Y($3,$4)) with lines lw 7 lc rgb locClr[i] notitle